/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovyVersion"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

def runAll = tasks.register("run.${project.name}.All") {
    group = 'Application'
}

// register run tasks for our Groovy scripts (run.pkg.ScriptName)
sourceSets.main.allSource.filter{ it.name.endsWith('.groovy') }.each { source ->
    var path = source.absolutePath
    if (path.contains('domain') || path.contains('util')) return
    var name = "${project.name}.${source.name - '.groovy'}"
    var subtask = tasks.register("run.$name", JavaExec) {
        dependsOn compileGroovy
        group = 'Application'
        description = "Run $source.name as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
        jvmArgs = ['--enable-native-access=ALL-UNNAMED'] // avoid warning on JDK25
    }
    runAll.configure {
        dependsOn subtask
    }
}
